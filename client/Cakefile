fs = require 'fs'
{exec} = require 'child_process'

srcDir = 'src'
outDir = 'app'
outFile = "#{outDir}/app.js"
buildDir = 'build'
buildFile = "#{buildDir}/app.coffee"

appFiles = [
  'app'
]

task 'build', 'Build application js file from coffee files', ->
  makeDirs()
  appContents = new Array
  remaining = appFiles.length
  # the weird looping, with remaining and process() at zero, is due
  # to node.js' async behavious
  for file, index in appFiles then do (file, index) ->
    fs.readFile "#{srcDir}/#{file}.coffee", 'utf8', (err, fileContents) ->
      throw err if err
      appContents[index] = fileContents
      process() if --remaining is 0
  process = ->
    fs.writeFile buildFile, appContents.join('\n\n'), 'utf8', (err) ->
      throw err if err
      exec "coffee -o #{outDir} -c #{buildFile}", (err, stdout, stderr) ->
        throw err if err
        console.log stdout + stderr
        fs.unlink buildFile, (err) ->
          throw err if err
          console.log 'Done.'

makeDirs = ->
  dirs = [outDir, buildDir]
  exec "mkdir -p #{dir}" for dir in dirs
